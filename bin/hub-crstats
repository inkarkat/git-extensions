#!/bin/bash
set -o pipefail

printShortUsage()
{
    # Note: short followed by long option; if the user knows the short one, she can
    # skim the long one.
    printf 'Usage: %q %s\n' "$(basename "$1")" '[--with-oldest-date] [--with-newest-date] [--ignore-label PATTERN [...]] [--only-label PATTERN [...]] [<issue-args> ...] [-?|-h|--help]'
}
printUsage()
{
    # This is the short help when launched with no or incorrect arguments.
    # It is printed to stderr to avoid accidental processing.
    printShortUsage "$1" >&2
    printf >&2 '\nTry %q --help for more information.\n' "$(basename "$1")"
}
printLongUsage()
{
    # This is the long "man page" when launched with the help argument.
    # It is printed to stdout to allow paging with 'more'.
    cat <<HELPDESCRIPTION
Show statistics of GitHub issues and pull requests.
HELPDESCRIPTION
    echo
    printShortUsage "$1"
    echo
    cat <<HELPTEXT
    --with-oldest-date	Prepend the relative age of the oldest issue / pull
			request, separated by a tab character.
    --with-newest-date	Prepend the relative age of the newest issue / pull
			request, separated by a tab character. This will always
			come after a requested oldest date, regardless of the
			option order.
    --ignore-label PATTERN
			Only consider issues / pull requests that do not have a
			label matching PATTERN.
    --only-label PATTERN
			Only consider issues / pull requests that have a label
			matching PATTERN. Other issues / pull requests (and
			those that also match --ignore-label) are ignored.
PATTERN is an AWK-style extended regular expression.
HELPTEXT
}

typeset -a issueArgs=()
isWithOldestDate=
isWithNewestDate=
ignoreLabelPattern=
onlyLabelPattern=
hubCrLabelFormat=
typeset -a postProcessor=()
while [ $# -ne 0 ]
do
    case "$1" in
	--help|-h|-\?)		shift; printLongUsage "$0"; exit 0;;
	--with-oldest-date)	shift; isWithOldestDate=t; postProcessor=(reldate);;
	--with-newest-date)	shift; isWithNewestDate=t; postProcessor=(reldate);;
	--ignore-label)		shift; hubCrLabelFormat='labels %L%n'; ignoreLabelPattern+="${ignoreLabelPattern:+|}${1:?}"; shift;;
	--only-label)		shift; hubCrLabelFormat='labels %L%n'; onlyLabelPattern+="${onlyLabelPattern:+|}${1:?}"; shift;;
	--)			issueArgs+=("$1"); break;;
	*)			issueArgs+=("$1"); shift;;
    esac
done


crstats()
{
    hub-cr --format "${hubCrLabelFormat}%ct %au%n%as%n" "${issueArgs[@]}" "$@" | awk \
	-F ',? +' \
	-v "isWithOldestDate=$isWithOldestDate" \
	-v "isWithNewestDate=$isWithNewestDate" \
	-v "ignoreLabelPattern=${ignoreLabelPattern//\\/\\\\}" \
	-v "onlyLabelPattern=${onlyLabelPattern//\\/\\\\}" \
	-v "me=$(git me-in-github)" \
'
function join(array, start, end, sep,    result, i)
{
    if (sep == "")
	sep = " "
    else if (sep == SUBSEP) # magic value
	sep = ""
    result = array[start]
    for (i = start + 1; i <= end; i++)
	result = result sep array[i]
    return result
}
function nameOrOtherCount(assignees, offset, prefix,      num, result, key, joiner) {
    num = length(assignees) + offset
    if (num <= 0) {
	return ""
    } else if (num <= 2) {
	result = prefix
	for (key in assignees) {
	    if (key == me) continue
	    result = result joiner key
	    joiner = ", "
	}
	return result
    } else {
	return prefix num " other" (num == 1 ? "" : "s")
    }
}
function allOrCount(count, total, suffix) {
    return (count == total ? (total <= 1 ? "" : "all ") : count " ") suffix
}
function stats(what, count, mineCount, assignedCount, assignedToUsCount, assignedToMeCount, otherAssignees, coAssignees,        aCnt, additionals) {
    if (mineCount) additionals[aCnt++] = allOrCount(mineCount, count, "from me")
    if (assignedCount) {
	additionals[aCnt++] = allOrCount(assignedCount, count, "assigned:") nameOrOtherCount(otherAssignees, 0, " to ")
	if (assignedToUsCount) additionals[aCnt++] = allOrCount(assignedToUsCount, assignedCount, "to me") nameOrOtherCount(coAssignees, -1, " and ")
	if (assignedToMeCount) additionals[aCnt++] = allOrCount(assignedToMeCount, assignedCount, "to me")
    }

    return sprintf("%s %s%s%s", \
	(count ? count : "no"), what, (count == 1 ? "" : "s"), \
	(aCnt ? " (" join(additionals, 0, --aCnt, ", ") ")" : "") \
    )
}
function recordDates(what) {
    if (oldestDate[what] == "") oldestDate[what] = 2147483647
    if (newestDate[what] == "") newestDate[what] = 0

    if ($2 < oldestDate[what]) oldestDate[what] = $2
    if ($2 > newestDate[what]) newestDate[what] = $2
}
function printDates(what) {
    if (isWithOldestDate) printf("@%d\t", oldestDate[what])
    if (isWithNewestDate) printf("@%d\t", newestDate[what])
}
function isAssignedAlsoToMe(otherAssignees, coAssignees,     i) {
    for (i = 1; i <= NF; i++) {
	if ($i == me) {
	    if (NF > 1) {
		recordAssignees(coAssignees)
		return 1
	    } else {
		return 0
	    }
	}
    }
    recordAssignees(otherAssignees)
    return 0
}
function isAssignedOnlyToMe() {
    return (NF == 1 && $1 == me)
}
function recordAssignees(assignees) {
    for (i = 1; i <= NF; i++) {
	assignees[$i] = 1
    }
}
function isLabelMatch(labelPattern,      i, labelLine, labelCnt, labels) {
    # Labels can have spaces, so we must only accept comma-separation here.
    labelLine = substr($0, 1 + length($1) + 1 + length($2) + 1)
    labelCnt = split(labelLine, labels, /, /)

    for (i = 1; i <= labelCnt; i++) {
	if (labels[i] ~ labelPattern) {
	    return 1
	}
    }
    return 0
}

$2 == "labels" {
    if ((ignoreLabelPattern != "" && isLabelMatch(ignoreLabelPattern)) ||
	(onlyLabelPattern != "" && ! isLabelMatch(onlyLabelPattern))) {
	getline
	getline
	next
    } else {
	# Add the prepended CR type to the next line, too, so that the non-label
	# parsing can remain ignorant of the label filtering.
	crType = $1
	getline
	$0 = crType " " $0
    }
}
$1 == "pull" {
    pullCnt += 1
    recordDates("pull request")
    if ($3 == me) pullMineCnt+=1
    if (getline == 1 && $0 != "") {
	pullAssignCnt += 1
	pullToUsCnt += isAssignedAlsoToMe(pullOtherAssignees, pullCoAssignees)
	pullToMeCnt += isAssignedOnlyToMe()
    }
}
$1 == "issue" {
    issueCnt += 1
    recordDates("issue")
    if ($3 == me) issueMineCnt+=1
    if (getline == 1 && $0 != "") {
	issueAssignCnt += 1
	issueToUsCnt += isAssignedAlsoToMe(issueOtherAssignees, issueCoAssignees)
	issueToMeCnt += isAssignedOnlyToMe()
    }
}
END {
    if (issueCnt) {
	printDates("issue")
	print stats("issue", issueCnt, issueMineCnt, issueAssignCnt, issueToUsCnt, issueToMeCnt, issueOtherAssignees, issueCoAssignees)
    }
    if (pullCnt) {
	printDates("pull request")
	print stats("pull request", pullCnt, pullMineCnt, pullAssignCnt, pullToUsCnt, pullToMeCnt, pullOtherAssignees, pullCoAssignees)
    }
}
'
}

eval 'crstats "$@"' \
    "${postProcessor:+|}" '"${postProcessor[@]}"'
